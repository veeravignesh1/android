import com.toggl.buildsrc.Libs

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'de.mannodermaus.android-junit5'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

kapt {
    correctErrorTypes = true
    useBuildCache = true
}

android {
    compileSdkVersion buildConfig.compileSdk

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode buildConfig.versionCode
        versionName buildConfig.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    buildTypes {
        adhoc {}
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        apiVersion = '1.3' // Compose compiler based on 1.4 but set 1.3 for compatibility
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerVersion Libs.AndroidX.Compose.compiler
        kotlinCompilerExtensionVersion Libs.AndroidX.Compose.version
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xallow-jvm-ir-dependencies", "-Xskip-prerelease-check"]
    }
}

dependencies {
    coreLibraryDesugaring Libs.Android.desugarJdkLibs
    api project(':common-android')
    api project(':architecture')
    api project(':repository')

    api Libs.Google.firebaseAnalytics
    api Libs.Microsoft.appCenterAnalytics
    api Libs.Google.gson
    api Libs.Square.retrofit
    api Libs.Square.gsonConverter
    implementation Libs.AndroidX.Compose.runtime
    implementation Libs.AndroidX.Compose.UI.core
    implementation Libs.AndroidX.Compose.UI.foundation
    implementation Libs.AndroidX.Compose.UI.animation
    implementation Libs.AndroidX.Compose.UI.icons
    implementation Libs.AndroidX.Compose.UI.material
    implementation Libs.AndroidX.Compose.UI.tooling
    implementation Libs.AndroidX.Compose.UI.layout

    implementation Libs.Hilt.hilt
    kapt Libs.Hilt.compiler

    testImplementation Libs.Test.Jupiter.api
    testRuntimeOnly Libs.Test.Jupiter.engine
    testImplementation Libs.Test.Jupiter.params
    testImplementation Libs.Test.kotestMatchers
    testImplementation Libs.slf4j
    testImplementation Libs.Test.mockk
    testImplementation Libs.Test.kotlinTest
    testImplementation Libs.Test.kotlinTestJunit
    testImplementation Libs.Test.kotlinCoroutineTest
    testImplementation Libs.Test.assertJ

    androidTestImplementation Libs.AndroidX.Test.runner
    androidTestImplementation Libs.AndroidX.Test.espressoCore
}
